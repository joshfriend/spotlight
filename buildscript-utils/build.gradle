
plugins {
  id 'org.jetbrains.kotlin.jvm'
  id 'com.vanniktech.maven.publish'
  id 'com.autonomousapps.testkit'
  id 'org.jetbrains.kotlinx.binary-compatibility-validator'
  id 'me.champeau.jmh'
}

group = 'com.fueledbycaffeine.spotlight'
version = VERSION_NAME

mavenPublishing {
  publishToMavenCentral(true)
  if (providers.gradleProperty("signingInMemoryKey").isPresent()) {
    signAllPublications()
  }

  pom {
    name = "Spotlight buildscript utils"
    description = "Parse and build a DAG of your gradle projects"
    inceptionYear = "2025"
    url = "https://github.com/joshfriend/spotlight/"
    licenses {
      license {
        name = "MIT License"
        url = "https://choosealicense.com/licenses/mit/"
        distribution = "https://choosealicense.com/licenses/mit/"
      }
    }
    developers {
      developer {
        id = "joshfriend"
        name = "Josh Friend"
        url = "https://github.com/joshfriend/"
      }
    }
    scm {
      url = "https://github.com/joshfriend/spotlight/"
      connection = "scm:git:git://github.com/joshfriend/spotlight.git"
      developerConnection = "scm:git:ssh://git@github.com/joshfriend/spotlight.git"
    }
  }
}

dependencies {
  api(libs.moshix.sealedRuntime)
  implementation(libs.moshi.kotlin)
  implementation(libs.moshix.sealedReflect)
  implementation(libs.okio)
  implementation(libs.kotlinx.coroutines.core)
  
  // Groovy AST parsing
  implementation(libs.groovy)
  
  // Kotlin PSI parsing
  implementation(libs.kotlin.compiler.embeddable)

  testImplementation gradleApi()
  testImplementation libs.assertk
  testImplementation platform(libs.junit.platform)
  testImplementation libs.junit.jupiter
  testRuntimeOnly libs.junit.launcher

  jmhImplementation(libs.jmh.core)
  jmhImplementation(libs.jmh.generator)
}

kotlin {
  explicitApi()
}

java {
  toolchain {
    languageVersion.set(JavaLanguageVersion.of(libs.versions.javaTarget.get()))
  }
}

test {
  useJUnitPlatform()
}

jmh {
  jmhVersion = libs.versions.jmh.get()
  resultFormat = "json"
//  humanOutputFile = layout.buildDirectory.file("results/jmh/human.txt")
  zip64 = true
  timeUnit = 'ms'
}
