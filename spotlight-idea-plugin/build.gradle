import org.jetbrains.intellij.platform.gradle.IntelliJPlatformType
import org.jetbrains.kotlin.gradle.dsl.JvmTarget

plugins {
  id 'java'
  id 'org.jetbrains.kotlin.jvm'
  id 'org.jetbrains.intellij.platform'
}

// Configure project's dependencies
repositories {
  mavenCentral()
  intellijPlatform {
    defaultRepositories()
  }
}

kotlin {
  compilerOptions {
    jvmTarget.set(JvmTarget.JVM_21)
  }
}

java {
  toolchain {
    languageVersion.set(JavaLanguageVersion.of(21))
  }
}

def intellijTarget = "2025.3.1"

dependencies {
  implementation project(":buildscript-utils")

  intellijPlatform {
    intellijIdea(intellijTarget)

    bundledPlugin("com.intellij.gradle")

    intellijPlatformTesting.runIde.register("runLocalIde") {
      // https://plugins.jetbrains.com/docs/intellij/android-studio.html#configuring-the-plugin-gradle-build-script
      def path = providers.gradleProperty("intellijPlatformTesting.idePath").getOrNull()
      if (path != null) {
        localPath.set(file(it))
      }
    }
  }
}

boolean isSnapshot() {
  return VERSION_NAME.endsWith("SNAPSHOT")
}

if (isSnapshot()) {
  version = "$VERSION_NAME-${System.currentTimeMillis()}"
} else {
  version = VERSION_NAME
}

// https://plugins.jetbrains.com/docs/intellij/tools-intellij-platform-gradle-plugin-extension.html
intellijPlatform {
  pluginConfiguration {
    name = "Spotlight"

    ideaVersion {
      sinceBuild = "251"
    }
  }

  publishing {
    token = providers.environmentVariable("JETBRAINS_MARKETPLACE_TOKEN")
      .orElse(providers.gradleProperty("jetbrainsMarketplaceToken"))
    channels = [ isSnapshot() ? "EAP" : "Stable" ]
  }

  pluginVerification {
    ides {
      ide(
        IntelliJPlatformType.IntellijIdea,
        intellijTarget
      )
    }
  }
}
