plugins {
  id 'java'
  id 'org.jetbrains.kotlin.jvm'
  id 'org.jetbrains.intellij.platform'
}

// Configure project's dependencies
repositories {
  mavenCentral()
  intellijPlatform {
    defaultRepositories()
  }
}

dependencies {
  implementation project(":buildscript-utils")

  intellijPlatform {
    intellijIdeaCommunity("2025.1.3")

    bundledPlugin("com.intellij.gradle")
  }
}

boolean isSnapshot() {
  return VERSION_NAME.endsWith("SNAPSHOT")
}

if (isSnapshot()) {
  version = "$VERSION_NAME-${System.currentTimeMillis()}"
} else {
  version = VERSION_NAME
}

// https://plugins.jetbrains.com/docs/intellij/tools-intellij-platform-gradle-plugin-extension.html
intellijPlatform {
  pluginConfiguration {
    name = "Spotlight"

    ideaVersion {
      sinceBuild = "242"
    }
  }

  publishing {
    token = providers.environmentVariable("JETBRAINS_MARKETPLACE_TOKEN")
      .orElse(providers.gradleProperty("jetbrainsMarketplaceToken"))
    channels = [ isSnapshot() ? "EAP" : "Stable" ]
  }

  pluginVerification {
    ides {
      recommended()
    }
  }
}
